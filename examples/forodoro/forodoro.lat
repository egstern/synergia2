! File fodo.lat
!
! Written for the January, 2007 USPAS.
! To be used in conjunction with CHEF.
!
! Send complaints to the author: Leo Michelotti
!
! Modified by James Amundson to increase the quadrupole length.
! ------------------
! Parameters
! ------------------
focus    :=   7                   ! [m]     : focal length of equivalent
                                  !         :   thin quad
sepn     :=  10                   ! [m]     : distance between quad centers
length   :=   2.0                 ! [m]     : quadrupole length
strength := 1/(focus*length)      ! [m**-2] : quadrupole strength
                                  !         :   = B'/brho
beam, particle=proton, energy=1.5

! ------------------
! Elements
! ------------------
o: drift, l=( sepn - length )/2
f: quadrupole, l=length, k1=strength
d: quadrupole, l=length, k1=(-strength)
r: rfcavity, l= 0.,  volt=  0.5

! ------------------
! Lattice
! ------------------
forodoro:  line=( f, o, r, o, d, o, r, o )
